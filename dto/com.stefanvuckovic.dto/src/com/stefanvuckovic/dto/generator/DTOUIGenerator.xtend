/*
 * generated by Xtext 2.10.0
 */
package com.stefanvuckovic.dto.generator

import com.stefanvuckovic.dto.dTO.DTOClass
import com.stefanvuckovic.dto.dTO.DTOModel
import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext

/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class DTOUIGenerator extends AbstractGenerator {
	
	override void doGenerate(Resource resource, IFileSystemAccess2 fsa, IGeneratorContext context) {
		val model = resource.allContents.toIterable.filter(DTOModel).head
		fsa.generateFile('''ui/mainTemplate.ui''', generateDefaultTemplate)
		for (c : model.concepts.filter(DTOClass)) {
			fsa.generateFile('''ui/«c.name»Edit.ui''', c.compileEditComponents)
			fsa.generateFile('''ui/«c.name»View.ui''', c.compileViewComponents)
		}
	}
	
	def compileViewComponents(DTOClass c) '''
		«c.compileViewServerComponent»
		«c.compileViewPage»
	'''
	
	def compileEditComponents(DTOClass c) '''
		«c.compileEditServerComponent»
		«c.compileEditPage»
	'''
	
	def compileViewServerComponent(DTOClass c) '''
		server_component «c.name»ViewSC {
		
		}
	'''
	
	def compileViewPage(DTOClass c) '''
		page «c.name»View(«c.name» «c.name.toFirstLower») uses «c.name»ViewSC sc template:mainTemplate {
			override body {
				«FOR attr : c.attributes»
				#label value:"«attr.name.splitCamelCaseAttrName»"
				output(«c.name.toFirstLower».«attr.name»)
				«ENDFOR»
			}
		}
	'''
	
	def compileEditServerComponent(DTOClass c) '''
		server_component «c.name»EditSC {
			void save();
		}
	'''
	
	def compileEditPage(DTOClass c) '''
		page «c.name»View(«c.name» «c.name.toFirstLower») uses «c.name»EditSC sc template:mainTemplate {
			override body {
				#form {
					«FOR attr : c.attributes»
					#label value:"«attr.name.splitCamelCaseAttrName»"
					input(«c.name.toFirstLower».«attr.name»)
					«ENDFOR»
					
					#action action : sc.save()
				}
			}
		}
	'''
	
	def generateDefaultTemplate() '''
		template mainTemplate {
			header {
			
			}
			body {
				
			}
		}
	'''
	
	def splitCamelCaseAttrName(String name) {
		val words = name.split("(?<!(^|[A-Z]))(?=[A-Z])|(?<!^)(?=[A-Z][a-z])")
		words.map[normalizeWord].join(" ")
	}
	
	def normalizeWord(String word) {
		if(word.length() > 1 && Character.isLowerCase(word.charAt(1))) {
			return word.toFirstLower
		}
		word;
	}
}
